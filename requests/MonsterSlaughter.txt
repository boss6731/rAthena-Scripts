//===== rAthena Script =======================================
//= Monster Slaughter
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0
//===== Tested With =========================================
//= rAthena SQL 07/16-2017 Revision
//===== Description ==========================================
//= Opens an warp portal every "OnMinuteXX" minute where lots of
//  mobs will await the players. The portal will stay opened for 10 mins.
//  The event will end until the next time the portal opens or
//  all mobs are defeated.
//= Reward for each kill is an item, ".tcg_id", TCG Card (7227) from which
//  the player recieve an random amount between 1 - ".tcg_am".
//= The mob ids are saved in the array ".mob"
//  The quantity is saved in the array ".am"
//===== Comments =============================================
//= Request by PapaZola (http://rathena.org/board/user/682-papazola/)
//============================================================
-	script	MonsterSlaughter	-1,{
end;

OnInit:
// 怪物 ID
setarray .mob[0],1037,1213,1201;
// 數量
setarray .am[0],50,50,50;
set .tcg_id,7227; // TCG ID
set .tcg_am,3; // 數量
set .map$,"force_3-1"; // 事件地圖
end;

OnMinute15: // 每 x 分鐘開始一次事件
set .mobs,0; // 重置計數器
mapwarp .map$,"prontera",150,180; // 將已在地圖上的玩家傳送到普隆德拉
killmonsterall .map$;
announce "怪物屠殺事件現在開始。如果你想參加，請前往主城的傳送門。",bc_all;
sleep 5000;
announce "此傳送門將保持 10 分鐘開放。",bc_all;
sleep 5000;
enablenpc "#sl_warp";
setmapflag .map$,mf_noskill;
setmapflag .map$,mf_nowarp;
setmapflag .map$,mf_nowarpto;
setmapflag .map$,mf_noteleport;
setmapflag .map$,mf_noreturn;
setmapflag .map$,mf_nodrop; // 不要掉落其他物品
for ( set .@i,0; .@i < getarraysize(.mob); set .@i,.@i + 1) {
	if( strmobinfo(1,.mob[.@i]) != "null" || strmobinfo(1,.mob[.@i]) != "" || .am[.@i] != 0) {
		monster .map$,0,0,strmobinfo(1,.mob[.@i]),.mob[.@i],.am[.@i],strnpcinfo(0)+"::OnSlaughtered";
		set .mobs,.mobs + .am[.@i]; // 將怪物數量添加到總數中
	} else debugmes strnpcinfo(1)+": .mob["+.@i+"] 的 ID 無效，或相應數量為 0，因此將不會被生成。";
}
sleep 600000; // 事件持續 10 分鐘
disablenpc "sl_warp";
end;

OnSlaughtered:
getitem .tcg_id,rand(1,.tcg_am); // 獲得隨機 (1 - .tcg_am) 張 TCG
set .mobs,.mobs - 1; // 減少計數器
if(.mobs == 0) { // 沒有怪物了
	removemapflag .map$,mf_noskill;
	removemapflag .map$,mf_nowarp;
	removemapflag .map$,mf_nowarpto;
	removemapflag .map$,mf_noteleport;
	removemapflag .map$,mf_noreturn;
	removemapflag .map$,mf_nodrop;
	mapannounce .map$,"所有怪物都被消滅了，你現在將被傳送回普隆德拉。",bc_all;
	announce .map$,"怪物屠殺事件結束。",bc_all;
	sleep 5000;
	mapwarp .map$,"prontera",150,180; // 將玩家傳送回普隆德拉
}
end;

}
prontera,155,165,0	script	#sl_warp	45,5,5,{
warp getvariableofnpc(.map$,"MonsterSlaughter"),0,0;
end;

OnInit:
disablenpc strnpcinfo(0);
end;
}
