
//===== rAthena Script =======================================
//= Gold Room Battle			GPT 翻譯 繁體中文 ====
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Initial Version
//= 1.1 - Fixed bugs and tested
//        Exchanged "atcommand @cleanmap" with script command "cleanmap"
//        Fixed the "cleanmap" command for it's updated use
//===== Tested With ==========================================
//= rAthena 04/26/2019 Revision
//= GIT Hash: 67e1583f9fec7ac95a954634f6b7600ebe428df0
//===== Description ==========================================
//= A battle over an gold room, which occures on every ".delay" days.
//  Players can visit their gold room for ".dur" minutes, there is no delay restriction.
//  See 'OnInit' for config.
//===== Comments =============================================
//= Request by Paulinds
//  http://rathena.org/board/topic/59504-list-of-my-script-request/
//============================================================
prontera,164,172,4	script	Gold Room Battle	100,{
// 哈囉，玩家名稱。你需要什麼幫助？
mes .n$;
mes "你好，"+ strcharinfo(0)+ "。";
mes "你需要什麼樣的幫助呢？";
if(getgmlevel() > .gm) 
	if(select("- GM 選單:- 玩家選單") - 2) goto GM_M;
mes "你想要了解關於進入黃金房間的資訊，還是關於黃金房間戰鬥的資訊？";
if(.timer == .delay) mes "或者你想參加戰鬥嗎？";
mes "下一場戰鬥將在 "+ (.delay-.timer) +" 天後舉行。";
next;
switch(select("- "+( ($gold_acc == getcharid(3))?"傳送到黃金房間":"所有權")+":- "+( (.GRB_Room == 1)?"開始戰鬥":"房間已關閉")+":- 資訊:- 沒事了") ) {
	
	case 1:
	mes .n$;
	if($gold_acc && ($gold_acc == getcharid(3)) ) {
		mes "你想進入黃金房間嗎？";
		mes "^FF0000注意：在 "+.dur+" 分鐘後，你將被自動傳送回你的儲存點。^000000";
		if(select("- 是的:- 不") - 1) close;
		next;
		mes .n$;
		mes "好的，我們開始吧。";
		close2;
		warp .gold$,.gold_x,.gold_y;
		sleep2( .dur*60*1000 ); // 分鐘*60 > 秒*1000 = sleep2所需的毫秒數
		doevent strnpcinfo(0)+"::OnDone";
	} else if($gold_acc != getcharid(3))
		mes "目前的所有者是 ^00FF00"+$gold_n$+"^000000。";
	break;
	
	case 2:
	mes .n$;
	if(!.GRB_Room) {
		mes "對不起，但戰鬥尚未開放。";
		close;
	}
	if(getmapusers(.pvp$) >= .limit) {
		mes "對不起，已達到 "+.limit+" 人的上限。";
		close;
	}
	mes "你要加入黃金房間戰鬥嗎？";
	if(select("- 是的:- 不") - 1) close;
	next;
	mes .n$;
	mes "我現在將你傳送到戰場。";
	close2;
	warp .pvp$,0,0;
	set @gold,1;
	sleep2 2000;
	if(getmapusers(.pvp$) == .limit) doevent strnpcinfo(0)+"::OnBattleStart";
	break;
	
	case 3:
	mes .n$;
	mes "[ === 戰鬥 === ]";
	mes "黃金房間戰鬥，正如其名，是一種 PvP 類型的活動，最後倖存者將獲得自己的黃金房間存取權，直到下一場戰鬥結束。";
	mes "整個活動的最長持續時間為 "+.min+" 分鐘。";
	mes "最多可容納 "+.limit+" 名玩家參加，報名時間為 3 分鐘。";
	mes "下一場戰鬥將在 "+.delay +" 天後舉行。";
	mes " ";
	mes "[ === 黃金房間 === ]";
	mes "戰鬥的勝利者可以在接下來的 "+.delay+" 天內以 "+.dur+" 分鐘的時間造訪他/她的黃金房間，沒有使用上的限制。";
	mes "這意味著沒有使用上的限制。";
	break;
	
	case 4:
	break;
}
end;

GM_M:
next;
mes .n$;
mes "現在該怎麼辦？";
if(select("- 開始/停止活動:- 沒事了") - 1) close;
next;
if(.GRB_Eve) doevent strnpcinfo(0)+"::OnDisable"; 
else doevent strnpcinfo(0)+"::OnPrepare";
mes .n$;
mes "活動已經 "+ ( (.GRB_Eve)?"關閉":"開啟")+"。";
close;

OnPCLogoutEvent:
if(getcharid(3) != $gold_acc) end;
OnDone:
cleanmap .gold$;
warp "SavePoint",0,0;
end;

OnPCLoadMapEvent:
if( (strcharinfo(3) == .pvp$ && !@gold) || (strcharinfo(3) == .gold$ && getcharid(3) != $gold_acc) ) {
	dispbottom .n$+": 由於你嘗試加入戰鬥但沒有事先註冊，或者你試圖造訪其他玩家的黃金房間，我將把你傳送回儲存點。";
	warp "SavePoint",0,0;
	end;
}
while(getmapusers(.gold$) != 0) {
	makeitem 969,1,.gold$,rand(88,111),rand(168,191);
	sleep2 1000;
}		
cleanmap .gold$;
end;

OnPrepare:
set .timer,.delay*60*24;
OnMinute00:
set .timer,.timer + 1;
if( (.timer/60)/24 >= .delay) {
	if(getmapusers(.pvp$) > 0) {
		mapannounce .pvp$,.n$+": 我將把你們全部傳送回普隆德拉，因為我需要這張地圖來進行黃金房間戰鬥。",bc_all;
		sleep 5000;
		mapwarp .pvp$,"prontera",150,180;
	}
	set .timer,0;
	set $gold_acc,0;
	announce .n$+": 黃金房間戰鬥即將開始！！！",bc_all;
	sleep 10000;
	announce .n$+": 你有 3 分鐘的時間進行報名，現在開始計時！！！",bc_all;
	initnpctimer;
	set .GRB_Eve,1;
	set .GRB_Room,1;
	if(getmapflag(.pvp$,mf_gvg) == 1) {
		set .gvg,1;
		removemapflag .pvp$,mf_gvg;
	}
	if(getmapflag(.pvp$,mf_pvp) == 1) {
		set .pvp,1;
		pvpoff .pvp$;
	}
	while(.GRB_Room) {
		waitingroom "當前參戰者: "+getmapusers(.pvp$),0;
		sleep 2000;
		delwaitingroom;
	}
}
end;

OnTimer120000: // 2 分鐘
announce .n$+": 快點，還剩 1 分鐘！！！",bc_all;
end;

Ontimer180000: // 3 分鐘
OnBattleStart:
stopnpctimer;
set .GRB_Room,0;
announce .n$+": 報名已截止，讓我們看看有多少人報名....",bc_all;
sleep 10000;
if(getmapusers(.pvp$) < 2) {
	announce .n$+": 看來參加者不夠，無法開始戰鬥。",bc_all;
	goto OnDisable;
}
setmapflag .pvp$,mf_noreturn;
setmapflag .pvp$,mf_nowarpto;
setmapflag .pvp$,mf_nowarp;
setmapflag .pvp$,mf_noteleport;
setmapflag .pvp$,mf_nosave;
setmapflag .pvp$,mf_nomemo;
setmapflag .pvp$,mf_partylock;
setmapflag .pvp$,mf_pvp_noparty;
setmapflag .pvp$,mf_pvp_noguild;
setmapflag .pvp$,mf_loadevent;
announce .n$+": 有 "+getmapusers(.pvp$)+" 名玩家報名參加。",bc_all;
sleep 5000;
announce .n$+": 我將在 10 秒內開始戰鬥！！參戰者，準備好了嗎！！祝大家好運！！",bc_all;
sleep 5000;
mapannounce .pvp$,.n$+": 還剩 5 秒！！！",bc_all;
sleep 1000;
mapannounce .pvp$,.n$+": 還剩 4 秒！！！",bc_all;
sleep 1000;
mapannounce .pvp$,.n$+": 還剩 3 秒！！！",bc_all;
sleep 1000;
mapannounce .pvp$,.n$+": 還剩 2 秒！！！",bc_all;
sleep 1000;
mapannounce .pvp$,.n$+": 還剩 1 秒！！！",bc_all;
sleep 1000;
mapannounce .pvp$,.n$+": 開始！！！",bc_all;
pvpon .pvp$;
startnpctimer;
set .t_ck,0; // Timer Check
end;

OnTimer360000: // 3 分鐘 = 180 秒（報名時間）+ 每分鐘 = 60 秒 > 360000 毫秒
stopnpctimer;
set .t_ck,.t_ck + 1;
if(.t_ck == .min/2) {
	mapannounce .pvp$,.n$+": "+.min/2+" 分鐘過去了！！",bc_all;
	if(getmapusers(.pvp$) == 0) {
		sleep 10000;
		mapannounce .pvp$,.n$+": 哎呀！玩家都去哪了？既然沒有玩家了，我會結束戰鬥。",bc_all;
		goto OnDisable;
	}
} else if(.t_ck == .min) {
	mapannounce .pvp$,.n$+": "+.min+" 分鐘過去了！！由於沒有人贏得這場戰鬥，我將結束這場戰鬥！！",bc_all;
	goto OnDisable;
}
setnpctimer 180100; // 重置計時器至報名時間過後
startnpctimer;
end;

OnDisable:
set .GRB_Room,0;
delwaitingroom;
removemapflag .pvp$,mf_noreturn;
removemapflag .pvp$,mf_nowarpto;
removemapflag .pvp$,mf_nowarp;
removemapflag .pvp$,mf_noteleport;
removemapflag .pvp$,mf_nosave;
removemapflag .pvp$,mf_nomemo;
removemapflag .pvp$,mf_partylock;
removemapflag .pvp$,mf_pvp_noparty;
removemapflag .pvp$,mf_pvp_noguild;
removemapflag .pvp$,mf_loadevent;
if(.gvg) {
	set .gvg,0;
	setmapflag .pvp$,mf_gvg;
}
if(!.pvp) pvpoff .pvp$;
else set .pvp,0;
announce .n$+": 黃金房間戰鬥活動已結束！！",bc_all;
mapannounce .pvp$,.n$+": 你們將在 10 秒內被傳送回普隆德拉。",bc_all;
sleep 10000;
mapwarp .pvp$,"prontera.gat",150,180;
set .GRB_Eve,0;
end;

OnPCKillEvent:
if(strcharinfo(3) != .pvp$ || !@gold ) end;
mapannounce .pvp$,.n$+": "+strcharinfo(0)+" 擊敗了 "+rid2name(killedrid)+"！！",bc_all;
end;

OnPCDieEvent:
if(strcharinfo(3) != .pvp$ || !@gold ) end;
set @gold,0;
warp "SavePoint",0,0;
sleep2 1500; // 減慢腳本速度
percentheal 100,100;
if(killerrid != getcharid(3)) dispbottom .n$+": 你被 "+rid2name(killerrid)+" 擊敗了！！";
if(getmapusers(.pvp$) == 2) mapannounce .pvp$,.n$+": 嗚呼！！只剩下 2 名玩家，讓我們看看誰是更強的玩家！！",bc_all;
else if(getmapusers(.pvp$) == 1) {
	announce .n$+": 黃金房間戰鬥的勝利者是 "+ rid2name(killerrid)+ "。",bc_all;
	set $gold_acc,killerrid; // 儲存勝利者的 RID
	set $gold_n$,rid2name($gold_acc);
	detachrid;
	if(attachrid($gold_acc) == 1) dispbottom .n$+": 你贏得了黃金房間戰鬥，如果你想進入你的黃金房間，請與我對話。";
	donpcevent strnpcinfo(0)+"::OnDisable";
} else if(getmapusers(.pvp$) == 0) donpcevent strnpcinfo(0)+"::OnDisable";
end;


OnInit:
set .n$,"["+strnpcinfo(1)+"]"; // NPC 名稱
set .limit,10; // 最大玩家數量
set .min,15; // 戰鬥持續時間
set .pvp$,"guild_vs5"; // PvP 房間地圖
set .gold$,"new_zone04"; // 黃金房間地圖
set .gold_x,100; // 傳送至黃金房間的 X 座標
set .gold_y,181; // 傳送至黃金房間的 Y 座標
// 如果 PvP 地圖不同於黃金房間地圖，
// 使用特殊的地圖標誌並設置變數以區分
set .diff,0;
if(.pvp$ != .gold$) { 
	setmapflag .gold$,mf_noreturn;
	setmapflag .gold$,mf_nowarp;
	setmapflag .gold$,mf_nomemo;
	setmapflag .gold$,mf_nosave;
	setmapflag .gold$,mf_nowarpto;
	setmapflag .gold$,mf_noteleport;
	setmapflag .gold$,mf_loadevent;
	set .diff,1;
}
set .delay,2; // 戰鬥之間的延遲天數。
set .dur,1;// 訪問黃金房間的時間（分鐘）
end;
}
