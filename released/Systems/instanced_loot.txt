//===== rAthena 腳本 ==========================================
//= 副本戰利品系統
//===== 作者 ===================================================
//= llchrisll
//===== 版本 ==================================================
//= 1.0 - 初始版本
//===== 測試版本 ===============================================
//= rAthena 2018年10月10日修訂版
//= GIT 哈希值：55acdb9863382d8935d9df25e1462d5d1ebd7d54
//===== 說明 ==================================================
//= 在副本運行後處理戰利品分配
//= 每個怪物掉落將自動被“拾取”
//  並保存在數據庫中
//= 成員可以選擇他們想要的戰利品
//= 戰利品將均勻分享
//= 奇數量的戰利品將隨機分配給想要該特定物品的成員
//===== 註釋 ==================================================
//= 安裝：
//  1. 通過複製在文件末尾添加副本地圖
//  2. 在每個有關怪物死亡的 NPC 事件中添加函數“F_InstancedLoot”
//  > callfunc "F_InstancedLoot",killedrid,getcharid(1);
//===== SQL 表格 ===============================================
/* phpMyAdmin：
CREATE TABLE IF NOT EXISTS `ilootsys` (
  `pty_id` int(5) UNSIGNED NOT NULL,
  `item_id` int(8) UNSIGNED DEFAULT NULL,
  `item_am` int(5) UNSIGNED DEFAULT NULL,
  KEY `pty_id` (`pty_id`),
  KEY `item_id` (`item_id`)
) ENGINE=InnoDB DEFAULT CHARSET=latin1;
*/
//===== 待辦事項 ===============================================
//= 思考一個檢查重量並可能自動存儲物品的好方法
//============================================================
prontera,147,172,5	script	副本戰利品	100,{
mes .n$;
mes "歡迎！";
if(!getcharid(1)) {
	mes "看來你沒有組隊，所以我無法幫助你！";
	close;
}
mes "我可以怎麼幫你？";
next;
switch(select("- 選擇戰利品:- 資訊:- 算了")) {
	case 1:
	query_sql "SELECT `item_id` , `item_am` FROM `ilootsys` WHERE `pty_id` = '"+getcharid(1)+"'",.@item_id,.@item_am;
	if(getarraysize(.@item_id) < 1) {
		mes .n$;
		mes "抱歉，沒有你可以選擇的戰利品。";
		close;
	}
	set .@pty_id,getcharid(1);
	for ( set .@p,0; .@p < getarraysize(.pty_id); set .@p,.@p + 1)
		if(.@pty_id == .pty_id[.@p]) break;
	
	if(.@p >= getarraysize(.pty_id)) 
		setarray .pty_id[.@p],.@pty_id;
		
	if(getarraysize(getd(".pty_aid_"+.@pty_id)) == 0) {
		getpartymember(.@pty_id),1;
		getpartymember(.@pty_id),2;
		for ( set .@a,0; .@a < $@partymembercount; set .@a,.@a + 1)
			if(isloggedin($@partymemberaid[.@a],$@partymembercid[.@a]) == 1)
				setarray getd(".pty_aid_"+.@pty_id+"["+getarraysize(getd(".pty_aid_"+.@pty_id))+"]"),$@partymemberaid[.@a];
	}
	if(getarraysize(getd(".items_"+getcharid(3))) != 0) {
		mes .n$;
		mes "你已經選擇了你的戰利品。";
		mes "請等待直到每個人都選擇了他們的物品。";
		close;
	}
	while(1) {
		set .@loot$,"";
		for ( set .@i,0; .@i < getarraysize(.@item_id); set .@i,.@i + 1) 
			set .@loot$,.@loot$ + "> "+.@item_am[.@i]+"x "+getitemname(.@item_id[.@i])+ ( (.@item_id[.@i+1] != 0)?":":"");

		if(.@loot$ != "") // 光學檢查，現有值？
			set .@loot$,.@loot$ + ":> 完成";
		else // 沒有插入物品，沒有其他可選
			set .@loot$,"> 完成";
		mes .n$;
		mes "請選擇您想要的戰利品：";
		mes "注意：數量是你的隊伍總共拾取的，而不是剩下的數量！";
		next;
		if(select(.@loot$) >= getarraysize(.@item_id)) { // 選擇了“完成”
			setarray .pty_ct[.@p],.pty_ct[.@p] + 1;
			if(getarraysize(getd(".items_"+getcharid(3))) == 0) // 選擇戰利品？沒有？
				setarray getd(".items_"+getcharid(3)+"[0]"),1; // 比使用額外變量更好
			break;
		}
		set .@c,@menu - 1;
		
		mes .n$;
		mes "已選物品：";
		mes getitemname(.@item_id[.@c]);
		mes " ";
		mes "這樣對嗎？";
		next;
		if(select("- 是的:- 不") - 1) continue;
		setarray getd(".items_"+getcharid(3)+"["+getarraysize(getd(".items_"+getcharid(3)))+"]"),.@item_id[.@c];
		deletearray .@item_id[.@c],1;
		deletearray .@item_am[.@c],1;
	}
	mes .n$;
	mes "現在我必須等待至少1分鐘，直到你隊伍的每個人都選擇了他們的戰利品。";
	if(getpartyleader(.@pty_id,1) == getcharid(3)) { // 如果附加的玩家是隊伍的領導者
		attachnpctimer; // 附加計時器到他/她
		initnpctimer;
	}
	break;
	
	case 2:
	mes .n$;
	mes "我將管理你在副本中的戰利品，通過均勻分配它們並根據你的隊友選擇分配。";
	mes "每個隊員將能夠從一份列表中選擇他/她想要的戰利品，在副本運行期間你已經收到了這份列表。";
	mes " ";
	mes "一旦每個成員都完成了這個任務，我將根據所掠取的物品計算出每個選擇該物品的玩家可以獲得多少。";
	mes "如果有奇數量的戰利品，最後一件物品將隨機分配給他們。";
	mes "例如：";
	mes "你拾取了10個Yellopy，有5個成員想要這個物品，所以每個人都能得到2個。";
	break;

	case 3: break;
}
close;

OnTimer60000: // 每分鐘		
stopnpctimer;
for ( set .@p,0; .@p < getarraysize(.pty_id); set .@p,.@p + 1)
	if(getcharid(1) == .pty_id[.@p]) break;
// 只是檢查以防止可能的錯誤
if(.@p >= getarraysize(.pty_id)) {
	debugmes "副本戰利品：附加的隊伍ID "+getcharid(1)+" 在 .pty_id 數組中未找到";
	end;
}
if(.pty_ct[.@p] != getarraysize(getd(".pty_aid_"+getcharid(1)))) {
	initnpctimer;
	end;
}
query_sql "SELECT `item_id` , `item_am` FROM `ilootsys` WHERE `pty_id` = '"+getcharid(1)+"'",.@item_id,.@item_am;
// 計算有多少玩家想要相同的物品：
for ( set .@c,0; .@c < getarraysize(getd(".pty_aid_"+getcharid(1))); set .@c,.@c + 1) 
	for ( set .@i,0; .@i < getarraysize(.@item_id); set .@i,.@i + 1) {
		for ( set .@l,0; .@l < getarraysize(getd(".items_"+getd(".pty_aid_"+getcharid(1)))); set .@l,.@l + 1) 
			if(getd(".items_"+getd(".pty_aid_"+getcharid(1)+"["+.@c+"]")+"["+.@l+"]") == .@item_id[.@i])
				setarray .@item_pl[.@i],.@item_pl[.@i] + 1;
		
		// 計算共享的戰利品量並刪除戰利品計數器
		if(.@item_pl[.@i]) { // 選擇的物品是想要的
			setarray .@item_share[.@i],.@item_am[.@i]/.@item_pl[.@i];
			if(.@item_am[.@i]%.@item_pl[.@i] > 0)
				setarray .@item_left[.@i],1;
		}
		deletearray .@item_pl[.@i],1;
	}
// 戰利品分配：
for ( set .@c,0; .@c < getarraysize(getd(".pty_aid_"+getcharid(1))); set .@c,.@c + 1) 
	for ( set .@i,0; .@i < getarraysize(.@item_id); set .@i,.@i + 1) {
		for ( set .@l,0; .@l < getarraysize(getd(".items_"+getd(".pty_aid_"+getcharid(1)))); set .@l,.@l + 1)
			// 檢查當前玩家是否選擇了當前物品
			if(getd(".items_"+getd(".pty_aid_"+getcharid(1)+"["+.@c+"]")+"["+.@l+"]") == .@item_id[.@i]) {
				if(.@item_am[.@i] >= .@item_share[.@i]) // 檢查是否還有戰利品可用，如果戰利品不足以滿足選擇它的玩家
					getitem .@item_id[.@i],.@item_share[.@i],getd(".pty_aid_"+getcharid(1)+"["+.@c+"]");
				setarray .@item_am[.@i],( (.@item_am[.@i] > .@item_share[.@i])?.@item_am[.@i]-.@item_share[.@i]:0); // 減少可用的物品數量 
				if(.@item_left[.@i]) // 如果有奇數量的戰利品，將帳戶ID保存在額外的數組中
					setarray getd(".@items_shared_"+.@item_id[.@i]+"["+getarraysize(getd(".@items_shared_"+.@item_id[.@i]))+"]"),getd(".pty_aid_"+getcharid(1)+"["+.@c+"]");
				deletearray getd(".items_"+getd(".pty_aid_"+getcharid(1)+"["+.@c+"]")+"["+.@l+"]"),1; // 從玩家的戰利品列表中刪除物品
			}
		if(.@item_left[.@i] > 0)
			getitem .@item_id[.@i],1,rand(getarraysize(getd(".@items_shared_"+.@item_id[.@i]))+"]");
	}
deletearray getd(".pty_aid_"+getcharid(1)+"[0]"),getarraysize(getd(".pty_aid_"+getcharid(1)));
deletearray .pty_id[.@p],1;
deletearray .pty_ct[.@p],1;
query_sql "DELETE FROM `ilootsys` WHERE `pty_id` = '"+getcharid(1)+"'";
npctalk .n$+"：戰利品分配完成！",strnpcinfo(0),bc_self;
end;

OnInit:
set .n$,"["+strnpcinfo(0)+"]";
//query_sql "DROP TABLE IF EXISTS `ilootsys`";
if(query_sql("SHOW TABLES LIKE 'ilootsys'",.@iloot$) == 0) {
	query_sql "CREATE TABLE IF NOT EXISTS `ilootsys` (  `pty_id` int(5) UNSIGNED NOT NULL,  `item_id` int(8) UNSIGNED DEFAULT NULL,  `item_am` int(5) UNSIGNED DEFAULT NULL, KEY `pty_id` (`pty_id`), KEY `item_id` (`item_id`) ) ENGINE=InnoDB DEFAULT CHARSET=latin1;";
	//query_sql "ALTER TABLE `ilootsys` ADD KEY `pty_id` (`pty_id`), ADD KEY `item_id` (`item_id`)";
}
set $@ilootsys,1;
end;
}

function	script	F_InstancedLoot	{
// getarg(0) == 隊伍ID
// getarg(1) == 怪物ID
if(!$@ilootsys) return;
if(getarraysize(getd("'items_"+getarg(1))) == 0) {
	getmobdrops(getarg(1));
	copyarray getd("'items_"+getarg(1)+"[0]"),$@MobDrop_item[0],$@MobDrop_count;
	copyarray getd("'rate_"+getarg(1)+"[0]"),$@MobDrop_rate[0],$@MobDrop_count;
}
for ( set .@d,0; .@d < getarraysize(getd("'items_"+getarg(1))); set .@d,.@d + 1)
	if(rand(1,10000) <= getd("'rate_"+getarg(1)+"["+.@d+"]")) {
		if(query_sql("SELECT `item_am` FROM `ilootsys` WHERE `pty_id` = '"+getarg(0)+"' AND `item_id` = '"+getd("'items_"+getarg(1)+"["+.@d+"]")+"'",.@i_am) == 0)
			query_sql "INSERT INTO `ilootsys` ( `pty_id` , `item_id` , `item_am` ) VALUES ( '"+getarg(0)+"' , '"+getd("'items_"+getarg(1)+"["+.@d+"]")+"' , '1')";
		else
			query_sql "UPDATE `ilootsys` SET `item_am` = `item_am` + '1' WHERE `pty_id` = '"+getarg(0)+"' AND `item_id` = '"+getd("'items_"+getarg(1)+"["+.@d+"]")+"'";
	}
return;
}
-	script	I_Loot	-1,{
end;

OnInstanceInit:
if(!$@ilootsys) end;
setmapflag instance_mapname(strnpcinfo(4)),mf_noloot;
if(getmapflag(instance_mapname(strnpcinfo(4)),mf_noloot) == 1)
	instance_announce 0,"[副本戰利品]：此副本使用副本戰利品系統，這意味著您的戰利品不會出現在地板上，但可以在主城的 NPC 那裡收集。",0;
end;

OnInstanceDestroy:
if(!$@ilootsys) end;
removemapflag instance_mapname(strnpcinfo(4)),mf_noloot;
end;
}
// 上面的 I_Loot 模板的副本，用於需要 OnInstanceInit 的地方
// 要添加一個副本，添加其中使用的地圖
//map_of_instance,0,0,0	duplicate(I_Loot)	I_Loot#1	-1
//map_of_instance,0,0,0	duplicate(I_Loot)	I_Loot#2	-1
//map_of_instance,0,0,0	duplicate(I_Loot)	I_Loot#3	-1
//map_of_instance,0,0,0	duplicate(I_Loot)	I_Loot#4	-1
