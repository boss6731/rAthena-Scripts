//===== rAthena 腳本 =======================================
//= 虛擬安全密碼系統
//===== 作者 ===================================================
//= llchrisll
//===== 版本與更改日誌 ==============================================
//= 1.0 - 初始版本
//= 	  做了一些改進（函數更改等）
//= 	  添加了密碼恢復功能
//= 1.1 - 修復了有關密碼檢查的一個小錯誤
//      - 我的修復不起作用，所以我改成了 "while()"，讓我們看看..
//= 1.2 - 優化和縮短，同時修復了在按下 "取消" / "請再試一次" 後出現 "未知 NPC"，
//		  這導致了錯誤。
//=1.2.1- 在登錄時出現了 "npc_checknear" 錯誤，所以...
//		- 對密碼恢復功能進行修改
//      - 添加了防止在登錄後使用 "ALT+M" 快捷鍵和在輸入安全密碼之前死於攻擊（如怪物或 PvP 地圖）的功能
//= 1.3 - 現在只使用 1 個函數 > VSP_FUNC
//      - 更改了函數的佈局
//      - 更改了 OnPCLoginEvent
//      - 刪除了選擇密碼類型的選項
//      - 用 "取消" 來替換 prompt()，以避免未知 PC 的情況
//      - 在玩家未能成功創建/檢查密碼後，添加了一分鐘後自動踢出 "addtimer"
//      - 添加了 IP 檢查
//      - 刪除了不必要的注釋
//= 測試過 =============================
//= rAthena SQL 07/16-2017 修訂版
//===== 描述 ==========================================
//= 這個系統允許您通過“虛擬鍵盤”以菜單樣式輸入您的第二個帳戶密碼
//= 可能的字符：
//= 字母（數組：.@m_letter$）
//= 數字（數組：.@m_numbers$）
//= 特殊字符（數組：.@m_specials$）> {,}，（）不可？
//=======================================================
prontera,164,175,4	script	VSP 管理員	110,{

if(#vsp_pw$ == "") set #vsp_pw_set,0; // 防止錯誤 o,o
mes .n$;
if(getgmlevel() >= 80) {
	if(#vsp_pw_set) {
		mes "想查看您的 VSP 詳細信息嗎？";
		if(select("- 不，謝謝:- 是的，請") - 1) {
			next;
			mes .n$;
			mes "您的密碼是：";
			mes #vsp_pw$;
			mes " ";
			mes "安全答案：";
			mes #sec_answer$;
		}
		next;
		mes .n$;
	}
}
if(!$vsp_sys) {
	mes "此系統目前已離線。";
	close;
}
mes "你好，"+strcharinfo(0)+"!";
mes "我能幫你什麼忙？";
next;
if(#vsp_pw_set) {
	switch(select("- 修改密碼:- 密碼恢復:- 沒事了，謝謝。")) {

		case 1:
		mes .n$;
		mes "您確定要修改您的密碼嗎？！";
		if(select("- 讓我們開始吧:- 最好不要") - 1) close;
		set .@vsp_old$,#vsp_pw$;
		while( .@temp_pw$ == "") 
			set .@temp_pw$,""+callfunc("VSP_FUNC",1,2);
		next;
		mes .n$;
		mes "您選擇的密碼是： " + .@temp_pw$;
		next;
		mes .n$;
		mes "這是正確的嗎？";
		if(select("- 是的:- 不是") - 1) close;
		next;
		mes .n$;
		if(.@temp_pw$ != .@vsp_old$) {
			mes "在我更改您的密碼之前，我需要您舊的密碼以保護您的安全。";
			while( .@pw_change$ == "") 
				set .@pw_change$,""+callfunc("VSP_FUNC",1,1);
			next;
			mes .n$;
			if(.@pw_change$ == .@vsp_old$) {
				mes "好的，您的舊密碼已確認。";
				mes "現在將使用您的新密碼。";				
				set #vsp_pw$,.@temp_pw$;
			} else {
				mes "密碼錯誤。";
				mes "舊的密碼將被重新使用。";
				set #vsp_pw$,.@vsp_old$;
			}
			set #vsp_pw_set,1;
		} else 
			mes "抱歉，但您又選擇了相同的密碼。";
		break;
			
		case 2:
		mes .n$;
		if(#sec_que > 0) {
			mes "您想要做什麼？";
			next;
			if(select("- 恢復我的舊密碼:- 更改安全問題/答案") == 1) 
				callfunc("VSP_FUNC",2,1);
					
			else {
				mes .n$;
				mes "首先，我需要檢查您的密碼。";
				while( .@temp_pw$ == "") 
					set .@temp_pw$,""+callfunc("VSP_FUNC",1,1);
				next;
				// ======= 密碼檢查完成 =========
				if(.@temp_pw$ == #vsp_pw$) {
					mes .n$;
					mes "感謝您確認您的身份。";
				} else {
					mes .n$;
					mes "所點擊的密碼無效。";
					close;
				}
				set #sec_que,0;
				while( !#sec_que )
					callfunc("VSP_FUNC",2,2);
				next;
				mes .n$;
				mes "您的安全問題已成功更改。";
			}
		} else {
			mes "在您恢復密碼之前，您必須先設置安全問題和答案。";
			while( !#sec_que )
				callfunc("VSP_FUNC",2,2);
			next;
			mes .n$;
			mes "您的安全問題已成功設置。";
		}
		break;
			
		case 3:	
		break;
	}
		
} else if(!#vsp_pw_set) {
	while( .@temp_pw$ == "" )
		set .@temp_pw$,""+callfunc("VSP_FUNC",1,2);
	next;
	//========== 確認密碼 =================
	mes .n$;
	mes "您選擇的密碼是： \""+.@temp_pw$+"\"";
	set #vsp_pw$,.@temp_pw$;
	set #vsp_pw_set,1;
	while( !#sec_que ) 
		callfunc("VSP_FUNC",2,2);
	next;
	mes .n$;
	mes "您的密碼和安全問題已成功設置。";
}
end;

OnInit:
set .n$,"[VSP 管理員]";
// 系統狀態：
// 1 = 在線
// 0 = 離線
set $vsp_sys,1;
end;
}
//?=============================* VSP 登錄檢查開始 *============================?//
-	script	VSP登錄	-1,{

OnPCLoginEvent:
//=================== 系統開啟? ====================
if(!$vsp_sys && !#vsp_pw_set) 
	end;
//======== 系統暫時關閉並設置變量 ========
else if(!$vsp_sys && #vsp_pw_set == 1) {
	announce "VSP 系統目前離線。",bc_self;
	set #vsp_pw_set,2;
	end;
//======== 系統恢復並設置變量 ========
} else if($vsp_sys && #vsp_pw_set == 2) {
	announce "VSP 系統已恢復在線。",bc_self;
	set #vsp_pw_set,1;
	end;
}
set .@n$,getvariableofnpc(.n$,"VSP 管理員");
sc_start 112,999999,1; // 狂暴狀態，無法說話/使用命令
pcblockmove getcharid(3),1; // 防止移動
atcommand "@battleignore"; // 防止玩家因攻擊而死亡
//============ 密碼已設置 ============
if(#vsp_pw_set) {
	query_sql "SELECT `last_ip` FROM `login` WHERE `account_id` = '"+getcharid(3)+"'",.@l_ip$;
	if(.@l_ip$ != #vsp_lastip$) {
		mes .@n$;
		mes "您好，玩家。";
		mes "在您能夠在服務器上遊玩之前，我必須確認您的密碼。";
		while( .@temp_pw$ == "") 
			set .@temp_pw$,""+callfunc("VSP_FUNC",1,1);
		next;
		mes .@n$;
		if(.@temp_pw$ != #vsp_pw$) {
			mes "所點擊的密碼無效。";
			mes "您將被踢出。";
			sleep2 2000;
			atcommand "@kick "+strcharinfo(0);
			end;
		}
		set #vsp_lastip$,.@l_ip$;
	}
//======== 創建密碼 ========
} else if(!#vsp_pw_set) {
	mes .@n$;
	mes "您好，"+strcharinfo(0)+"。";
	mes "為了您的安全，我想請您為您的帳戶設置第二個密碼。";
	mes "如果您不按照這些步驟操作，您將被踢出。";
	next;
	mes .@n$;
	mes "該系統可以通過“虛擬鍵盤”輸入您的密碼。";
	while( .@temp_pw$ == "" )
		set .@temp_pw$,""+callfunc("VSP_FUNC",1,2);
	next;
	//========== 確認密碼 =================
	mes .@n$;
	mes "您選擇的密碼是： \""+.@temp_pw$+"\"";
	set #vsp_pw$,.@temp_pw$;
	set #vsp_pw_set,1;
	while( !#sec_que ) 
		callfunc("VSP_FUNC",2,2);
	next;
	mes .@n$;
	mes "您的密碼和安全問題已成功設置。";
	query_sql "SELECT `last_ip` FROM `login` WHERE `account_id` = '"+getcharid(3)+"'",#vsp_lastip$;
}
mes "感謝您確認您的身份。";
sc_end 112;
pcblockmove getcharid(3),0;
atcommand "@battleignore";
percentheal 100,100;
end;

OnTimer60000:
message strcharinfo(0),"[VSP 管理員]: 因為您對密碼的創建/檢查花費了太長時間，我現在將踢出您。";
sleep2 2000;
atcommand "@kick "+strcharinfo(0);
end;
}
//?===================================* VPS 函數 *===================================?//
// getarg(0):
// 1 = 密碼
// 2 = 安全問題/答案
// getarg(1):
// 1 = 檢查
// 2 = 創建
//?======================================================================================?//
function	script	VSP_FUNC	{
	set .@n$,getvariableofnpc(.n$,"VSP 管理員");
	next;
	mes .@n$;
	if(getarg(0) == 1) {
		// 1 分鐘後自動踢出如果沒有在此期間內完成密碼檢查/創建
		attachnpctimer;
		initnpctimer "VSP登錄";
		setarray .@m_letter$[0],"A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z","a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z";
		setarray .@m_numbers$[0],"0","1","2","3","4","5","6","7","8","9";
		setarray .@m_specials$[0],"`","´","'","§","%","&","[","]","!","=","?";
		set .@pw_max_len,10; // 最大密碼長度
		if(getarg(1) == 1) {
			mes "如果您忘記了密碼：";
			mes "輸入\"FFFF\"/\"ffff\"或\"1111\"，其餘都可以。";
			next;
		} else if(getarg(1) == 2) {
			mes "現在請告訴我您的密碼長度。";
			mes "但最大長度為 "+.@pw_max_len+" 個字符。";
			if(input(#pw_lenght,0,.@pw_max_len) != 0) return;
			next;
			mes .@n$;
			mes "所以您的密碼長度為 "+#pw_lenght+" 個字符？";
			next;
		}
		mes .@n$;
		mes "現在點擊下面的字符以顯示您要的。";
		mes "注意：您有 1 分鐘的時間來完成此操作，否則您將被踢出！";
		
	// =========== 菜單創建 ===== 
		set .@pw_counter,0;
		set .@m_choice,1;
		for(set .@p,0; .@p < getarraysize(.@m_letter$); set .@p,.@p + 1) 
			set .@menu_pw$,.@menu_pw$ + "- "+.@m_letter$[.@p]+ ( (.@m_letter$[.@p+1] == "")?"":":");

		while(.@pw_counter != #pw_lenght) {

		// ======== 設置菜單顯示選項 ========
			if(.@m_choice == 1) {
				set .@m_option$,":- 數字:- 特殊字符";
				set .@sel_m,getarraysize(.@m_letter$);

			} else if(.@m_choice == 2) {
				set .@m_option$,":- 字母:- 特殊字符";
				set .@sel_m,getarraysize(.@m_numbers$);

			} else if(.@m_choice == 3) {
				set .@m_option$,":- 字母:- 數字";
				set .@sel_m,getarraysize(.@m_specials$);
			}
			set .@menu$, .@menu_pw$ + .@m_option$ + ":- 請再試一次";

	// ======= 菜單顯示 + 選擇 ======	
			set .@vsp_m,select(.@menu$);

	// ======================== "取消" 按鈕或 "再試一次" - 選項 ========
			if(.@vsp_m > (.@sel_m + 2))
				return "";
				
			else if(.@vsp_m <= .@sel_m) {
				set .@pw_c,.@vsp_m-1;
				if(.@m_choice == 1) 
					set .@temp_pw$,.@temp_pw$ + .@m_letter$[.@pw_c];
				else if(.@m_choice == 2) 
					set .@temp_pw$,.@temp_pw$ + .@m_numbers$[.@pw_c];
				 else if(.@m_choice == 3)
					set .@temp_pw$,.@temp_pw$ + .@m_specials$[.@pw_c];
					
				set .@pw_counter,.@pw_counter + 1;
					
			} else if(.@vsp_m > .@sel_m  && (.@vsp_m < (.@sel_m + 3))) {
	// ======== 選擇對應菜單選項後設置新的字符菜單 ========
				set	.@menu_pw$,"";
				if(.@m_choice == 1) {
					if(.@vsp_m == (.@sel_m + 1)) {
						for(set .@p,0; .@p < getarraysize(.@m_numbers$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_numbers$[.@p] + ( (.@m_numbers$[.@p+1] == "")?"":":");

						set .@m_choice,2;

					} else if(.@vsp_m == (.@sel_m + 2)) {
						for(set .@p,0; .@p < getarraysize(.@m_specials$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_specials$[.@p] + ( (.@m_specials$[.@p+1] == "")?"":":");

						set .@m_choice,3;
					}
				} else if(.@m_choice == 2) {
					if(.@vsp_m == (.@sel_m + 1)) {
						for(set .@p,0; .@p < getarraysize(.@m_letter$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_letter$[.@p] + ( (.@m_letter$[.@p+1] == "")?"":":");
						set .@m_choice,1;
					} else if(.@vsp_m == (.@sel_m + 2)) {
						for(set .@p,0; .@p < getarraysize(.@m_specials$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_specials$[.@p] + ( (.@m_specials$[.@p+1] == "")?"":":");
						set .@m_choice,3;
					}
				} else if(.@m_choice == 3) {
					if(.@vsp_m == (.@sel_m + 1)) {
						for(set .@p,0; .@p < getarraysize(.@m_letter$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_letter$[.@p] + ( (.@m_letter$[.@p+1] == "")?"":":");
						set .@m_choice,1;
					} else if(.@vsp_m == (.@sel_m + 2)) {
						for(set .@p,0; .@p < getarraysize(.@m_numbers$); set .@p,.@p + 1) 
							set .@menu_pw$, .@menu_pw$ + "- "+.@m_numbers$[.@p] + ( (.@m_numbers$[.@p+1] == "")?"":":");
						set .@m_choice,2;
					}
				}
				set .@menu$, .@menu_pw$ + .@m_option$ + ":- 請再試一次";
			}
		}
		return .@temp_pw$;
	}
	if(getarg(0) == 2) {
		next;
		mes "您想要設置或更改安全問題/答案嗎？";
		if(select("- 是:- 不") - 1) return;

		else {
			next;
			mes .@n$;
			if(getarg(1) == 1) {
				mes "您想要恢復您的舊密碼？";
				next;
				if(select("- 是的:- 不") - 1) 
					return "";

				else {
					mes .@n$;
					mes "請問您設置的安全問題是什麼？";
					mes "注意：不要告訴我您的答案！！";
					mes "如果您不記得問題，請告訴我，我將幫助您恢復。";
					input #sec_que,0,"";
				}
			} else if(getarg(1) == 2) {
				mes "現在請告訴我您的新安全問題。";
				input #sec_que,0,"";
				set #sec_que,1;
				next;
			}
		}
	}
}
//?=================================================================================?//
