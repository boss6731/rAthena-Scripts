//===== rAthena Script =======================================
//= In-Game Control Panel		GPT 翻譯 繁體中文 =====
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Initial Version
//		- Added Reset Equip
//		- Added Change Slot
//		- Fixed Change Slot Bug
//        Changed from Menu > input
//= 1.1 - Fixxed minor Bugs
//= 1.2 - Fixxed small bug, Showing Online Player was bugged
//		- Also reversed order of menu outputs (was pissing me off q.q)
//= 1.3 - Script Optimization
//      - Exchanged Edit Gender of Account to Char specific
//      - Added .char_gender to set delay for change gender per Character
//      - Fixxed Reset Equip, which didn't work because of getarg(0)
//===== Tested With =========================================
//= rAthena SQL 07/16-2017 Revision
//===== Description ==========================================
//= This Script allows Player's editing their Account in-Game
//  like an Control Panel.
//= For GM's over Level 40:
//- View Server Statistic
//- View your own Account - Same as Player
//- View other Char's Account Details by typing
//  the name of an character bounded to it; such us E-Mail, IP,
//  for GM's with Level 80 and higher Username and Account ID as well
//===== Comments =============================================
//= .max_ch: Max Characters per Account, please
//  put the number you have or the Slot Change Part
//  won't work like it should.
//  For Renewal Servers : src/common/mmo.h > #define MAX_CHARS
//  Don't know for older servers anymore though.
//===== ToDO =================================================
//= Adding an feature to delete accounts
//============================================================
prontera,147,166,6	script	In-Game CP	110,{

mes .n$;
mes "您好，" + strcharinfo(0);
mes "我可以告訴您您的帳戶";
mes "設置，同時我也可以";
mes "為您更改它們。";
mes "您想做什麼？";
next;
if(getgroupid() >= 40) goto GM_Menu;
if(select("- 顯示我的信息:- 什麼也不做") == 1) goto CP_My;
close;

//================== GM_Menu ==================
GM_Menu:
switch(select("- 查看玩家帳戶:- 查看我的帳戶:- 查看服務器統計信息")) {

//===================== 查看其他玩家帳戶 =====================
	case 1:
	mes .n$;
	mes "請輸入您要查找的角色名，我將找到與其綁定的帳戶。";
	input .@s_char$;
	next;
	mes .n$;
	if(.@s_char$ == "") { mes "角色名無效。"; close;}
	query_sql "SELECT `account_id` FROM `char` WHERE `name` = '"+.@s_char$+"'",.@s_accid;
	if(!.@s_accid) { mes "角色名無效。"; close;}
	query_sql "SELECT `userid` , `sex` , `email` , `logincount` , `last_ip` FROM `login` WHERE `account_id` = '"+.@s_accid+"'",.@user$,.@sex$,.@email$,.@login,.@last_ip$;
	if(getgmlevel() >= 80) {
		mes "用戶名: " + .@user$;
		mes "帳戶 ID: " + .@s_accid;
	}
	mes "性別: " + .@sex$;
	mes "電子郵件: " + .@email$;
	mes "登錄次數: " + .@login;
	mes "最後 IP: " + .@last_ip$;
	mes " ";
	mes "Duo 使用者保護";
	mes ((getgmlevel() >= 80) ? "不顯示密碼" : "不顯示用戶名、帳戶 ID 和密碼");
	mes "。";
	close;
//===================================================
	
//================= 查看我的帳戶 =============
	case 2:
	CP_My:
	query_sql "SELECT `userid` , `sex` , `email` , `logincount` , `last_ip` FROM `login` WHERE `account_id` = '"+getcharid(3)+"'",.@user$,.@sex$,.@email$,.@login,.@last_ip$;
	mes .n$;
	mes "用戶名: " + .@user$;
	mes "帳戶 ID: " + getcharid(3);
	mes "性別: " + .@sex$;
	mes "電子郵件: " + .@email$;
	mes "登錄次數: " + .@login;
	mes "您的 IP: " + .@last_ip$;
	mes " ";
	mes "Duo 使用者保護";
	mes "不顯示密碼。";
	mes "現在您想做什麼？";
	next;
	switch(select("- 查看角色:- 修改電子郵件:- 什麼也不做")) {
		
//============== 查看角色 ============
		case 1:
		mes .n$;
		mes "如果您想進行更改，請選擇一個角色。";
		query_sql "SELECT `char_id` , `char_num` , `name` , `class` , `base_level` , `job_level` , `zeny` , `str` , `agi` , `vit` , `int` , `dex` , `luk` , `last_map` , `last_x` , `last_y` , `save_map` , `save_x` , `save_y` FROM `char` WHERE `account_id` = '"+getcharid(3)+"' ORDER BY `base_level` DESC",.@char_id,.@char_num,.@name$,.@class,.@base_level,.@job_level,.@zeny,.@str,.@agi,.@int,.@vit,.@dex,.@luk,.@last_map$,.@last_x,.@last_y,.@save_map$,.@save_x,.@save_y;
		mes " ";
		for( set .@a,0; .@a < getarraysize(.@name$); set .@a,.@a + 1) {
			mes "^FF0000~ "+.@name$[.@a]+" ~^000000";
			mes "---------------------";
			mes "[基礎/職業: " + .@base_level[.@a] + "/" + .@job_level[.@a] + "]";
			mes "(職業: " + jobname(.@class[.@a]) + ")";
			mes "=====================";
			set .@menu$,.@menu$ + "- " + .@name$[.@a] + ( (.@name$[.@a] != "")?":":"");
		}
		next;
		set .@m,select(.@menu$) - 1;

		mes .n$;
		mes "您選擇了:";
		mes "^FF0000~ "+.@name$[.@m]+" ~^000000";
		mes "基礎/職業: " + .@base_level[.@m] + "/" + .@job_level[.@m];
		mes "職業: " + jobname(.@class[.@m]);
		mes "Zeny: " + .@zeny[.@m];
		mes " ";
		mes "力量: " + .@str[.@m];
		mes "敏捷: " + .@agi[.@m];
		mes "體力: " + .@vit[.@m];
		mes "智力: " + .@int[.@m];
		mes "敏捷: " + .@dex[.@m];
		mes "運氣: " + .@luk[.@m];
		mes " ";
		mes "位置:";
		mes "當前地圖: " + .@last_map$[.@m];
		mes "座標 x: " + .@last_x[.@m] + ", y: " + .@last_y[.@m];
		mes " ";
		mes "保存點:";
		mes "地圖: "+ .@save_map$[.@m] + " x: " + .@save_x[.@m] + ", y: " + .@save_y[.@m];
		mes "您想做什麼？";
		next;
		switch(select("- 重置位置:- 重置風格:- 更改角色槽:- 重置裝備:- 修改性別:- 什麼也不做")) {
			
//=============================== 重置位置 ============================
			case 1:
			mes .n$;
			query_sql "SELECT `online` FROM `char` WHERE `name` = '"+escape_sql(.@name$[.@m])+"'",.@on;
			if(.@on) {
				mes "抱歉，當您在此角色上線時，我無法重置";
				mes "選擇角色的位置。";
				close;
			}
			mes "我現在將重置位置。";
			query_sql "UPDATE `char` SET `last_map` =  '"+.@save_map$[.@m]+"' , `last_x` = '"+.@save_x[.@m]+"' , `last_y` = '"+.@save_y[.@m]+"' WHERE `name` = '"+escape_sql(.@name$[.@m])+"'";
			break;
				
//=============================== 重置風格 ============================					
			case 2:
			mes .n$;
			mes "這將重置您的";
			mes "整體風格。";
			if(select("- 繼續:- 停止！") - 1) close;
			next;
			mes .n$;
			query_sql "SELECT `online` FROM `char` WHERE `name` = '"+escape_sql(.@name$[.@m])+"'",.@on;
			if(.@on) {
				mes "抱歉，當您在此角色上線時，我無法重置";
				mes "選擇角色的風格。";
				close;
			}
			query_sql "UPDATE `char` SET `hair` = '0' , `hair_color` = '0' , `clothes_color` = '0' WHERE `name` = '"+escape_sql(.@name$[.@m])+"'";
			mes "您的請求已完成。";
			break;
				
//=============================== 更改角色槽 ============================					
			case 3:
			mes .n$;
			mes "已使用的角色槽：";
			mes " ";
			for( set .@num,0; .@num < getarraysize(.@char_num); set .@num,.@num + 1) {
				mes .@char_num[.@num] + ". " + .@name$[.@num];
			}
			mes "剩餘空槽: "+.max_ch - getarraysize(.@char_num);
			mes " ";
			mes "您想要更改到哪個角色槽？";
			next;
			input .@num_ch;

			mes .n$;
			if(.@num_ch > .max_ch) {
				mes "抱歉，您輸入了無效的數字。";
				mes "請再試一次。";
				close;
			}
			mes "您已選擇:";
			mes .@num_ch + ".";
			mes " ";
			mes "我現在將更改角色槽。";
			mes "是否正確？";
			if(select("- 是，繼續:- 否，錯誤") - 1) close;
			next;
			mes .n$;
			query_sql "SELECT `online` FROM `char` WHERE `name` = '"+escape_sql(.@name$[.@m])+"'",.@on;
			if(.@on) {
				mes "抱歉，當您在此角色上線時，我無法更改";
				mes "選擇角色的槽位。";
				close;
			}
			for(set @n_ct,0; @n_ct < getarraysize(.@char_num); set @n_ct,@n_ct + 1) 
				if(.@char_num[@n_ct] == .@num_ch) {
					mes "抱歉，但此槽位已被使用。";
					mes "請再試一次。";
					close;
				}
			query_sql "UPDATE `char` SET `char_num` = '"+.@num_ch+"' WHERE `name` = '"+escape_sql(.@name$[.@m])+"'";
			mes "您的請求已完成。";
			break;

//================================ 重置裝備 ===============================
			case 4:
			mes .n$;
			mes "要重置裝備嗎？";
			if(select("- 是，請:- 不，謝謝") - 1) close;
			next;
			mes .n$;
			query_sql "SELECT `online` FROM `char` WHERE `name` = '"+.@name$[.@m]+"'",.@on;
			if(.@on) {
				mes "抱歉，當您在此角色上線時，我無法重置";
				mes "此角色的裝備。";
				close;
			}
			mes "我現在開始。";
			query_sql "UPDATE `inventory` SET `equip` = '0' WHERE `char_id` = '"+.@char_id[.@m]+"'";
			query_sql "UPDATE `char` SET `weapon` = '0' , `shield` = '0' , `head_top` = '0' , `head_mid` = '0' , `head_bottom`  = '0' WHERE `name` = '"+escape_sql(.@name$[.@m])+"'";
			next;
			mes .n$;
			mes "您的請求已完成。";
			break;
			
//================================ 修改性別 ===============================
			case 5:
			mes .n$;
			if(getgroupid() > 0)
				if(select("- 重置性別延遲:- 什麼也不做") == 1)
					set char_gender,0;
			if(gettimetick(2) < char_gender) {
				mes "您不能再次更改您的性別。剩餘時間:";
				mes ""+callfunc("Time2Str",char_gender);
				close;
			}
			mes "您想更改角色的性別嗎？";
			mes "注意: 更改性別的間隔時間為:";
			mes ""+callfunc("Time2Str",.char_gender+gettimetick(2));
			if(select("- 是:- 否") - 1) close;
			next;
			mes .n$;
			mes "性別將在您關閉此窗口後更改。";
			mes "如果您在選擇的角色上線，您將被踢出。";
			close2;
			set char_gender,gettimetick(2) + .char_gender;
			changecharsex(.@char_id[.@m]);
			end;
			
//===================== 什麼也不做 =====================					
			case 6:
			break;
		} // 修改角色 - 結束
		break;
		
//============== 修改郵件 ============			
		case 2:
		mes .n$;
		mes "現在輸入新的電子郵件地址：";
		mes "注意: 輸入“取消”以取消您的操作。";
		next;
		input .@mail_n$;
		if(.@mail_n$ == "Cancel") goto CP_Quit;
		mes .n$;
		mes "舊電子郵件:" +.@email$;
		mes "新電子郵件:" +.@mail_n$;
		mes " ";
		mes "這正確嗎？";
		next;
		if(select("- 是，繼續:- 否，取消") - 1) {
			mes .n$;
			mes "按您的意願。";
			mes "您的請求已取消。";
			close;
		} else {
			mes .n$;
			mes "您的電子郵件將被更改。";
			next;
			query_sql "UPDATE `login` SET `email` = '"+escape_sql(.@mail_n$)+"' WHERE `account_id` = '"+getcharid(3)+"'";
			mes .n$;
			mes "您的請求已完成。";
			close;
		}
//===================== 什麼也不做 =====================			
		case 3:
		break;
	}
	break;
	
//=============== 服務器統計信息 ===================
	case 3:
	query_sql "SELECT count(*) FROM `login` WHERE `account_id` > 1",.@count_acc;
	query_sql "SELECT count(*) FROM `char`",.@count_char;
	query_sql "SELECT `zeny` FROM `char` WHERE `zeny` > 0",.@count_zeny;
	for( set .@z,0; .@z < getarraysize(.@count_zeny); set .@z,.@z + 1) 
		set .@total_zeny,.@total_zeny + .@count_zeny[.@z];
		
	next;
	mes .n$;
	mes "總帳戶數: " + .@count_acc;
	mes "總角色數: " + .@count_char;
	mes "總 Zeny: " + .@total_zeny;
	next;
	if(select("顯示在線玩家:- 什麼也不做") - 1) break;
	mes .n$;
	mes "我現在將在您的聊天框中顯示";
	mes "在線玩家列表。";
	close2;
	dispbottom "======~-[)* 在線 <> 玩家 *(]~-========";
	query_sql "SELECT `name` , `class` , `base_level` , `job_level` FROM `char` WHERE `online` = '1'",.@name$,.@class,.@b_level,.@j_level;
	for( set .@a,0; .@a < getarraysize(.@name$); set .@a,.@a + 1) 
		dispbottom .@name$[.@a] + " [基礎/職業: " + .@b_level[.@a] + "/" + .@j_level[.@a] + "] (職業: " + jobname(.@class[.@a]) + ")";
	end;
}
//=================== 退出消息 ==================
next;
mes .n$;
mes "有什麼其他問題嗎？";
close;
CP_Quit:
mes .n$;
mes "您已退出。";
close;
}
